plugins {
  id 'java'
  id 'maven'
  id 'signing'
  id 'net.minecrell.licenser' version '0.3'
}

group 'net.kyori'
version '1.0.1-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
  options.compilerArgs += ['-Xlint:all', '-Xlint:-path', '-parameters']
  options.deprecation = true
  options.encoding = 'UTF-8'
}

license {
  header project.file('header.txt')
  include '**/*.java'
  newLine false
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'https://oss.sonatype.org/content/groups/public/'
  }
}

dependencies {
  compile 'com.google.guava:guava:23.6-jre'
  compile 'com.neovisionaries:nv-websocket-client:2.8'
  compile 'com.squareup.okhttp3:okhttp:3.10.0'
  compile 'it.unimi.dsi:fastutil:8.1.1'
  compile 'net.kyori:cereal:1.0.5-SNAPSHOT'
  compile 'net.kyori:event-api:3.0.0'
  compile 'net.kyori:kassel:1.0.1-SNAPSHOT'
  compile 'net.kyori:mu:1.0.0-SNAPSHOT'
  compile('net.kyori:peppermint:1.0.6-SNAPSHOT') {
    exclude group: 'com.google.code.gson', module: 'gson'
  }
  compile 'net.kyori:violet:2.0.0-SNAPSHOT'
  compile 'org.slf4j:slf4j-api:1.7.25'
}

task javadocJar(type: Jar) {
  classifier 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

signing {
  required { !project.version.endsWith('-SNAPSHOT') && gradle.taskGraph.hasTask(':uploadArchives') && project.hasProperty('signing.keyId') }
  sign configurations.archives
}

jar {
  manifest.attributes(
    'Automatic-Module-Name': 'net.kyori.polar',
    'Implementation-Version': project.version + getGitHash()
  )
}

if(project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
  uploadArchives {
    enabled = System.getenv('TRAVIS') == null || project.version.endsWith('-SNAPSHOT')

    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
          description 'An implementation of kassel.'
          name project.name
          url 'https://github.com/KyoriPowered/polar/'

          developers {
            developer {
              name 'kashike'
            }
          }

          issueManagement {
            system 'GitHub Issues'
            url 'https://github.com/KyoriPowered/polar/issues'
          }

          licenses {
            license {
              name 'MIT License'
              url 'https://opensource.org/licenses/MIT'
            }
          }

          scm {
            connection 'scm:git@github.com:KyoriPowered/polar.git'
            developerConnection 'scm:git@github.com:KyoriPowered/polar.git'
            url 'https://github.com/KyoriPowered/polar/'
          }
        }
      }
    }
  }
}

def getGitHash() {
  def process = 'git rev-parse --short HEAD'.execute(null as String[], projectDir)
  process.waitFor()
  return '-' + (process.exitValue() ? 'unknown' : process.text.trim())
}
